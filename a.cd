⎕IO ⎕ML ⎕WX←0 1 3
'cache' 'memo' ⎕CY 'dfns'

wri←{ntie←{0::⎕SIGNAL ⎕EN ⋄ 22::⍵ ⎕NCREATE 0 ⋄ 0 ⎕NRESIZE ⍵ ⎕NTIE 0}⍺
 size←⍵⎕NAPPEND ntie,⎕DR ⎕AV ⋄ 1:rslt←size⊣⎕NUNTIE ntie}
vzz←0∊=∘⌊⍨∧1 ¯1 0∊⍨⍤15 0×
Scn←{2>≢⍵:⍵ ⋄ ↑⊃(⊢,⍨∘⊂⍺⍺∘⊃)/(⊂∘⊂∘⊃∘⌽,⍨¯1∘↓)⊂⍤¯1⊢⍵}
hex←{⍺←⌈/⌈2⍟1+,⍵ ⋄ '0123456789abcdef'[2⊥⍉(⊢((4,⍨⊢)⍴(4×⊢)↑⊣)∘⌈÷∘4∘≢),⍉(⍺⍴2)⊤,⍵]}
h2b←,∘⍉(4⍴2)⊤'0123456789abcdef'∘⍳
fft←{q←1+2⍟n←2÷⍨≢⍵ ⋄ v←1↓⍴⍵ ⋄ u←×\1,1↓⊢⍴∘*∘○0J1∘÷
 f←⊣((+⌿⊢),[.5](u⊣)×[0](-⌿⊢))⊢⍴⍨2,v,⍨⊣,n÷⊣ ⋄ (⍴⍵)⍴⊃f/(2*⍳q),⊂⍵}
ift←{q←1+2⍟n←2÷⍨≢⍵ ⋄ v←1↓⍴⍵ ⋄ u←×\1,1↓⊢⍴∘*∘○0J¯1∘÷
 f←⊣((+⌿⊢),[.5](u⊣)×[0](-⌿⊢))⊢⍴⍨2,v,⍨⊣,n÷⊣ ⋄ (≢⍵)÷⍨(⍴⍵)⍴⊃f/(2*⍳q),⊂⍵}
 
rho←{⍺←0∘⌷ ⋄ ⍺⍴⍤1 ¯1⊢⍵}
mta←rho⍨∘rho⍪3↓⊢
bas←65536|⊣⌿
ful←bas⍪1∘↓
par←1∘↓⍪⍨65536(|+×∘×)⊣⌿
isr←=∘|⍨⊃
isb←∧/∘,0 1∊⍨⊢
isn←{(isr≤⊃=⊃∘bas)⍵:2⌷⍵ ⋄ (2⌷⍵)≠0>⌊⌿3↓⍵}
zer←{isr⍵:∧⌿0=3↓⍵ ⋄ b←⊃bas⍵ ⋄ 0=(⊢+b×⊣)⌿3↓⍵}
top←(+⌿∧⍀)0=3∘↓
ovr←top+1∘⌷
pla←{⍺←⊢ ⋄ 1≡⍺1: ¯3+≢⍵ ⋄ k←(0⌊top+⍺+3-≢)⍵ ⋄ (k(+@1)3↑⍵)⍪(-⍺)↑k⊖3↓⍵}
mov←{k←(3-≢⍵)⌈(¯3+≢⍵)⌊⍺-1⌷⍵ ⋄ (⍺@1⊢3↑⍵)⍪(3∘-∘≢↑k⊖3∘↓↑⍨2×3-≢)⍵}

big←{⍺←11⍴16 ⋄ ~vzz ⍵: (⊃∘bas⍺)⍪0⍪(0>⍵)⍪⍵⊤∘|⍨(pla⍴⊃∘bas)⍺
 b←(⊃∘|∘bas ⍺)×0J1*(isr ⍺)≤(0∊⊢=9∘○)⍵ ⋄ k←¯1+(⌊32×2⍟⍨|b)-⌊(|b)⍟|⍵+0=⍵
 (pla⍺)pla⍣(~isr b)⊢b⍪k⍪(0>⍵×isr b)⍪(b⍴⍨(3×~∘isr b)+pla ⍺)⊤⌊0.1J.1+(b*k)××∘×⍣(isr b)⍨⍵}

sg0←3∘↓×⍤¯1 15∘×.5-2∘⌷
sg1←{b←⊃bas ⍵ ⋄ ~isr⍵: ⍵
 (0≠3⌷⍵)(≠@2)(3↑⍵)⍪|(3↓⍵)-⍨¯1↓(b∘×-1∘⊖)(0≠3⌷⍵)∧⍤15 ¯1⊢⊖∨⍀⊖0<0⍪⍨3↓⍵}
sg2←{b←⊃bas ⍵ ⋄ isr⍵: (3↑⍵)⍪(3↓⍵)-¯1↓(b∘×-1∘⊖)(0≠3⌷⍵)⍪0⍪⍨¯1↓∧⍀(b-1)=3↓⍵
 k←top ⍵ ⋄ k-←+⌿(|b)≤|2 3 4(b⊥↑)⍤0 15⊢k⊖3↓⍵ ⋄ (3↑⍵)⍪(-k)⊖(4∘↓⍪⍨b⊥4∘↑)k⊖3↓⍵}
cry←{b←⊃bas ⍵ ⋄ b=⊃⍵: (¯3+≢⍵)pla sg1(3↑⍵)⍪b⊤+∘(0⌷(0,b)∘⊤)Scn⊢(-∘≢↑3∘↓)⍵
 f←+⌿1 0(↓⍤¯1)(0,b)∘⊤ ⋄ g←0∘⌈+∘⊖∘(+⍀)∘⊖(1-b)×0∘⌊ ⋄ k←(3×~isr ⍵)+2+⌈(|b)⍟¯3+≢⍵
 (¯3+≢⍵)pla sg2 (3↑⍵)⍪f⍣k⊢g⍣(isr⍵)(-∘≢↑3∘↓)⍵}
       
flo←{n←pla ⍵ ⋄ k←(-n)⌈n⌊(0⌊1⌷⍵)-1⌷⍵ ⋄ z←×0.5-2⌷⍵ ⋄ b←⊃bas⍵
 isr⍵: cry⍣(b=⊃⍵)⊢(0(⌊@1)3↑⍵)⍪((n-1)∘↑⍪(n-1)∘⌷+0>z×∘(⌈⌿)n∘↓)k⊖(2×n)↑3↓⍵
 cry⍣(b=⊃⍵)⊢(0(⌊@1)3↑⍵)⍪((n-1)∘↑⍪(n-1)∘⌷+∘(((+⌿∧⍀)0∘=)(⌊b÷⍨(0J¯1*⊣)×0⌷⊖)⊢)n∘↓)k⊖(2×n)↑3↓⍵} 
cel←{n←pla ⍵ ⋄ k←(-n)⌈n⌊(0⌊1⌷⍵)-1⌷⍵ ⋄ z←×¯0.5+2⌷⍵ ⋄ b←⊃bas⍵
 isr⍵: cry⍣(b=⊃⍵)⊢(0(⌊@1)3↑⍵)⍪((n-1)∘↑⍪(n-1)∘⌷+0>z×∘(⌈⌿)n∘↓)k⊖(2×n)↑3↓⍵
 cry⍣(b=⊃⍵)⊢(0(⌊@1)3↑⍵)⍪((n-1)∘↑⍪(n-1)∘⌷+∘(((+⌿∧⍀)0∘=)(⌈b÷⍨(0J¯1*⊣)×0⌷⊖)⊢)n∘↓)k⊖(2×n)↑3↓⍵}
 
red←{cry((0J1*~isr⍵)×⌊/|0⌷⍵)⍪1↓⊃⍺⍺/⊂[¯1↓⍳≢⍴⍵]par⍵}
scn←{cry(0⌷⍵)⍪1↓(¯1⌽⍳≢⍴⍵)⍉↑⍺⍺\⊂[¯1↓⍳≢⍴⍵]par⍵}
rdf←{cry((0J1*~isr⍵)×⌊⌿|0⌷⍵)⍪1↓⊃⍺⍺/⊂[1~⍨⍳≢⍴⍵]par⍵}
scf←{cry(0⌷⍵)⍪1↓(1 0,2↓⍳≢⍴⍵)⍉↑⍺⍺\⊂[1~⍨⍳≢⍴⍵]par⍵}
dot←{⍺⍺rdf(0 1,⍨(2+⌽⍳¯2+≢⍴⍺),(≢⍴⍺)+⍳¯2+≢⍴⍵)⍉(⍺⍉⍨1⌽⌽⍳≢⍴⍺)⍵⍵⍤2⍤2 15⊢⍵⍉⍨(¯2+≢⍴⍵)⌽⍳≢⍴⍵}
out←{(0,⍨(⌽1+⍳¯1+≢⍴⍺),(≢⍴⍺)+⌽⍳¯1+≢⍴⍵)⍉(⍉⍺)⍺⍺⍤1⍤1 15⍉⍵}
pop←{⍺←⊢ ⋄ 1≡⍺1:cry(0⌷⍵)⍪1↓⍺⍺⍣⍵⍵⊢par⍵ ⋄ cry(0⌷⍵)⍪1↓⊃⍺⍺⍣⍵⍵/par¨⍺⍵}
rop←{⍺←⊂ ⋄ f←(⊢⌈0⌊⊣-⍨⊢×0>⊣)∘(1-⍴∘⍴) ⋄ v←⊃,/1-⍵⍵f¨⍺⍵
 g1←⊢⍉⍨f(↑,⍨¯1⌽↓)∘⍳∘⍴∘⍴⊢ ⋄ g2←⊢⍉⍨f(↑,⍨1⌽↓)∘⍳∘⍴∘⍴⊢ ⋄ h←~∘isb∧1∘=∘≡
 ⍬≡⍴⍺⍵: (v-1)g2⍣(h z)⊢z←⍺⍺⍤v⊢⍵⍵ g1 ⍵ ⋄ (¯1+⊃⌈/v)g2⍣(h z)⊢z←⊃⍺⍺⍤v/⍵⍵g1¨⍺⍵} 
 
rea←{isr ⍵: ⍵
 (cry(|0∘⌷)⍪1↓3∘↑⍪9○3∘↓×(⌽1 0J1 ¯1 0J¯1⍴⍨¯3+≢)∘.×0J¯1*4⊤1∘⌷)⍵}
ima←{isr ⍵: (3↑1↑⍵)mta 0⍴⍨⍴⍵
 (cry(|0∘⌷)⍪1↓3∘↑⍪11○3∘↓×(⌽1 0J1 ¯1 0J¯1⍴⍨¯3+≢)∘.×0J¯1*4⊤1∘⌷)⍵}
r2c←(0J1 1 0×[0]3∘↑)⍪sg0×(⌽1 0J¯1 ¯1 0J1⍴⍨¯3+≢)∘.×0J1*4⊤1∘⌷
com←{((0J1*(isr⍺)⍲isr⍵)×(|⊃⍺)⌊|⊃⍵)⍪(((ovr⍺)⌊ovr⍵)⌊(1⌷⍺)⌈1⌷⍵),[¯.5]0}
ch0←sg0(1⌷⊣)mov rho⍨∘rho⍨
cnj←{isr ⍵: ⍵ ⋄ (cry 3∘↑mta+×[0]∘⌽1 ¯1⍴⍨≢)⍵}
neg←{isr ⍵:(~@2)⍵ ⋄ (3∘↑⍵)⍪-3↓⍵}
add←{m←⍺ com ⍵ ⋄ cry m⍪(m ch0 r2c⍣((isr⍵)<isr⍺)⊢⍺ )+m ch0 r2c⍣((isr⍺)<isr⍵)⊢⍵}
sub←{⍺←⊢ ⋄ 1≡⍺1: neg⍵ ⋄ m←⍺ com ⍵
 cry m⍪(m ch0 r2c⍣((isr⍵)<isr⍺)⊢⍺)-m ch0 r2c⍣((isr⍺)<isr⍵)⊢⍵} 
 
mul←{a←r2c⍣((isr⍵)<isr⍺)⊢⍺ ⋄ w←r2c⍣((isr⍺)<isr⍵)⊢⍵
 m←((0J1*(isr⍺)⍲isr⍵)×(|⊃⍺)⌊|⊃⍵)⍪((1⌷⍺)+1⌷⍵),[¯.5](((isr⍺)∧isr⍵)∧(2⌷⍺)≠2⌷⍵)
 (¯3+≢⍵)pla cry m⍪¯1⊖⌊0.1J0.1+ift⊃×⍤¯1/fft¨(-2*⌈2⍟¯7+2×≢⍵)↑¨3∘↓¨a w}
di0←{k←⌊16÷2⍟|b←⊃bas ⍵ ⋄ c←~isr ⍵
 (k+2)pla⍣c⊢(0⌷⍵)⍪(k+(¯3+≢⍵)-1⌷⍵)⍪(2⌷⍵)⍪(b⍴⍨2+k+4×c)⊤⌊(b*2×k)÷b⊥k↑3∘↓ ⍵}
di1←{k←2×¯3+≢⍵ ⋄ (k pla ⍺)(⊢mul((0⌷⍵)⍪(-k+2)↑2⍴⍨⍴1↑⍵)sub mul)k pla ⍵}
in0←{k←1+⌈2⍟(¯3+≢⍵)÷⌊16÷2⍟|b←⊃bas ⍵ ⋄ (¯3∘+∘≢ pla (⊢di1 pop k di0)∘(ovr mov ⊢))⍵}
∆in ← cache''
inv←in0 memo ∆in
div←{⍺←⊢ ⋄ 1≡⍺1: inv⍵ ⋄ ⍺ mul inv⍵}
di2←{0≠2|⊃bas ⍵: (⊢div bas,2⊤⍨¯1∘+∘≢⍴bas)⍵
 (¯3∘+∘≢pla∘cry(0 1 0+[0]3∘↑)⍪((3⍴1),2÷⍨⊃∘bas)+.×(⌽∘⍳4)↓⍤¯1(2,⍨3⍴bas)⊤-∘≢↑3∘↓)⍵}
sq0←{k←⌊32÷2⍟|b←⊃bas ⍵ ⋄ c←~isr ⍵
 (k pla 0∘⌷⍪((⌊2÷⍨k)+∘⌊2÷⍨1∘⌷)⍪0⍪(b⍴⍨k+4×c)⊤zer+∘⌊(b*⌊k÷2)×.5*⍨b⊥k↑(-2|1∘⌷)⊖0⍪⍨3∘↓)k pla⍵}
sq1←{k←2×¯3+≢⍵ ⋄ (k pla ⍺)(di2 div add ⊢)k pla ⍵}
sqr←{k←1+⌈2⍟(¯3+≢⍵)÷⌊32÷2⍟|⊃bas ⍵
 (0∘⌷⍪~∘zer(×⍤15 ¯1)1↓¯3∘+∘≢pla ⊢ sq1⍣k sq0)(ovr mov⊢)r2c⍣(∨/,2⌷⍵)⊢⍵}
exp←{b←⊃bas⍵ ⋄ k←2⍟|b ⋄ s←(⌈/∘⌈k×0,∘,1+pla-ovr)⍵
 r←(1⍳⍨⊢<∘(⊢+∘(+\)(|b)∘⍟)1+⍳)(pla⍵)+⌈s÷k ⋄ n←3+(pla⍵)+⌈s÷k ⋄ z←b,1↑⍨-2+n
 (pla ⍵)pla mul⍨⍣s⊢z add mul∘(z∘add)red(⊢div out (1+⍳r)big⍨⊢)di2⍣s⊢n pla⍵}
snh←pla pla∘(di2 exp sub exp∘sub)≢pla⊢
cis←{(pla⍵)pla exp r2c⍣(isr⍵)(≢pla 2∘↑⍪0⍪0J1×sg0)⍵}
tan←{z←(0J1×|⊃bas⍵)⍪1↑⍨-2+≢⍵
 (pla⍵)pla rea⍣(isr⍵)cry(3∘↑⍪0J1×3∘↓)(z∘sub div z∘add)cis(cry 3∘↑⍪2×sg0)(≢pla⊢)⍵}
sin←{(pla⍵)pla rea⍣(isr⍵)(cry 3∘↑⍪0J¯1×3∘↓)(di2 cis sub cis∘sub)(≢pla⊢)⍵}

cat←⍪⍤¯1
rav←,⍤¯1
trn←⍉⍤15 ¯1
rot←⌽⍤15 ¯1
rof←⊖⍤15 ¯1
pic←⊃⍤¯1
sqd←⌷⍤15 ¯1
eql←zer sub
neq←~eql
lth←isn sub
gth←lth⍨
geq←~lth
leq←~gth

min←{z←⍺ geq ⍵ ⋄ cry((⊃⍺)⌊⊃⍵)⍪1↓(⍺×⍤15 ¯1⍨~z)+z×⍤15 ¯1⊢⍵}
max←{z←⍺ leq ⍵ ⋄ cry((⊃⍺)⌊⊃⍵)⍪1↓(⍺×⍤15 ¯1⍨~z)+z×⍤15 ¯1⊢⍵}
abs←{isr⍵: (2↑⍵)⍪0⍪3↓⍵ ⋄ (¯3∘+∘≢ pla sqr∘rea∘(mul∘cnj⍨⊢pla⍨4×∘⌊4÷⍨≢))⍵}
eps←{⍺←⊢ ⋄ 1≡⍺1:∊⍤¯1⊢⍵ ⋄ ⍉∨/(⍉⍺)eql⍤1⍤1 15⍉rav⍣(⍬≡rho ⍵)⊢⍵}
ind←{1(⍳⍨)⍤1⍉(⍺⍉⍨¯1⌽⍳⍴⍴⍺){∧/,(⍉⍺)eql(⍉⍵)}⍤(¯1+⍴⍴⍺)⍤((¯1+⍴⍴⍺),15)⊢(⍵⍉⍨¯1⌽⍳⍴⍴⍵)}
rol←{z←(0∘⌷⍪¯3∘+∘≢⍪0⍪3↓∘?⍴⍴bas)⍵ ⋄ (3↓⍵)≡3↓0⍴⍨⍴⍵:z ⋄ (0⌷⍵)⍪1↓(z((⊣×⍤¯1 15zer)+∘flo mul)⍵)}
tke←(⊃0⌷⊢)⍪1↓↑⍤15 ¯1
drp←(⊃0⌷⊢)⍪1↓↓⍤15 ¯1
spl←(⍉⊂⍤2)⊢⍉⍨1⌽⌽∘⍳∘⍴∘⍴
mix←{r←⌽⌈⌿↑,⌽∘rho¨⍵ ⋄ n←1+⍴⍴⍵
 (⊢⍴⍨1↓⍴)⍣(⍬≡⍴⍵)(⊢⍉⍨(1⌽⍳n)⍪n↓⍳∘⍴∘⍴)↑((⌈/∘,¯3∘+∘≢¨)pla¨⊢)(⊂r)tke¨⍵ rho⍨¨(⊢,¨⍨1⍴¨⍨(⍴r)-⍴¨)rho¨⍵}

bc0←2⍪(1∘⌷×2⍟bas)⍪2∘⌷⍪3∘↓(⍉∘(,⍤2)∘⍉⊤)⍨2⍴⍨2⍟⊃∘bas
bc1←{⍺=2:⍵ ⋄ k←2⍟⍺ ⋄ d←(1⌷⍵)+k⊤-1⌷⍵
 ⍺⍪(d÷k)⍪(2⌷⍵)⍪⍉2(⊥⍤2)⍉(⊢⍴⍨1∘↓∘⍴,⍨k,⍨k÷⍨≢)3∘↓d mov⍵pla⍨⌈/,k×⌈k÷⍨(k⊤-1⌷⍵)+pla⍵}
bc2←(0 mov⊢pla⍨≢-(⌊/0,∘,1∘⌷))∘(⊢↑⍨≢+(⌈/0,∘,1∘⌷))
bc3←{~1∊0<1⌷⍵:⍬ ⋄ k←⌈/(|bas,⍺)⍟,|bas ⍵
 0 sqd⊃(⌷⍤1 mul⊢)/((⌽⊢⊤⍨2⍴⍨∘⌊2∘⍟)⌈/,0⌈1⌷⍵),⊂(⊢,[0.5]mul⍨)par div cry ⍺⍪(⊃∘bas↑⍨∘-2+∘⌈k×pla)⍵}
bc4←{⍬≡⍺:⍵ ⋄ ⍺(⊣mul pla⍨∘pla⍨)⍵}
bch←{(isr ⍺)<isr ⍵:cry⍺⍪1↓r2c⍵∇⍨|⍺ ⋄ b2 b1←|∘bas¨(,⍺)⍵
 ∧/,b1=b2:⍺⍪1↓⍵ ⋄ (1=⍴∪∊b1)∧∧/=∘⌊⍨2⍟∊b1 b2: b2 bc1 bc0 ⍵ ⋄ k←⌈/(|bas,⍺)⍟,|bas ⍵
 f←(⊣pla⍨k+∘⌈⍨∘pla⊢)add(k+∘⌈⍨∘pla⊢)pla (3↑⊢)⍪(bas⍵)(×⍤15 ¯1)3↓⊢
 (2⌷⍵)@2⊢(⌈k×pla⍵)pla(⍺bc3⍵)bc4 f rdf ⍺⍪⍉¯3↑⍤0⍉⊖3↓ bc2 ⍵} 
 
a64←'ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/'
v64←0∊∊∘a64
b64←{⍺←64 ⋄ ~v64 ⍵: ⍺bch 64⍪0⍪0⍪a64 ⍳⍤15 0⊢⍵ ⋄ a64⌷⍨⍤15 0⊢3↓64 bch ⍵}
bin←3↓∘flo 2∘bch

mod←{r←⍺(⊢sub⊣mul∘flo div⍨)⍵ ⋄ (isr ⍺)⍱isr ⍵:r ⋄ r add (3↑⍺) mta ⍺×⍤¯1 15⊢(isn r)-r geq ⍺}
mx0←{z←par ⍺⍺ ⋄ cry(3↑⍵)mta 0 sqd⊃(z mod sqd mul⊢)/((⌽⊢↑⍨1-(+/∨\))bin ⍺),⊂(⊢,[0.5]mul⍨)par ⍵}
mex←{⍉(⍉⍺){⍺((0⌷⍵)mx0)1⌷⍵}⍤1 2⊢⍉⍺⍺,[¯.5]⍤¯1⊢⍵}
pi0←{⍵ pla div red 0⌷⍤1⊢⊃(cry(3↑⊢) mta +.×⍨)/(⊂⍤2(1 0,[0.5]⍨⍤1(1+2×⊢),[0.5]1,⍨¯1↓×⍨)⌽⍳⌈1.5×⍵),⊂par ⍺⍪0⍪0⍪(⍺⍴⍨⌈⍵×⍟⍵)⊤2 2⍴0 4 1 0}
∆pi ← cache''
pi1←pi0 memo ∆pi
pie←⊢ mul |∘⊃∘bas pi1 ¯3∘+∘≢
pmu←{v←1⌈¯7 ¯1+(2↑⍴⍺)+2↑⍴⍵ ⋄ m←((0J1*(isr⍺)⍲isr⍵)×(|⊃⍺)⌊|⊃⍵)⍪(⍉(1⌷v)⍴⍤0⊢⍉(0⌷1⌷⍺)+(0⌷1⌷⍵)),[¯.5]0
 (¯3+≢⍵)pla cry m⍪(-1⌷v)↑[1]¯1⊖[1]¯1⊖⌊0.1J0.1+ift ift⍤¯1⊃×⍤¯2/fft⍤¯1¨fft¨(⊂-2*⌈2⍟v)↑¨sg0¨⍺⍵} 

e02←1∊0>1∘⌷,[¯.5]pla-1∘⌷
d01←-~∘e02⌷¯1∘+∘top,[¯0.5]1∘⌷
de0←,/∘⍕¨
de1←⊣,'.',⊢
de2←↓de1∘de0⍨∘de0⍨↑
de3←⊢↓⍨∘(+/∧\)' ¯0'∊⍨⊢
de4←⊢(/⍨)∘⌽∘(∨\)∘⌽∘~∊∘'.0 '
de5←⊢,⍨' 0'⌷⍤0 15⍨('.'=⊃)⍤1
e03←(↑⍕¨)¯1∘+∘top-1∘⌷
e04←⊣,⍨'E',⍨⊢
n01←' ¯'⌷⍨⍤15 0isn
dec←{10≠|⊃bas⍵:∇(⊢bch⍨10××∘⊃∘bas)⍵ ⋄ ~isr⍵: (∇rea⍵),'J',∇ima⍵
 ~∘' '⍤1⊢(n01⍵),de5 de4∘de3⍤1⊢(e03 ⍵)e04⍣(e02 ⍵)⊢(d01(de2⍤0 1)¯1∘⌽∘⍳∘⍴∘⍴⍉3∘↓)⍵}

pr0←{⍵∊0 1:⍬⋄⎕IO←1⋄⍬(⍵{⍺⍺<×⍨⊃⌽⍺:⍺,⍵⋄(⍺,N)∇⍵/⍨0≠⍵|⍨N←⊃⍵})1↓⍳⍵}
∆pr ← cache''
pr1←pr0 memo ∆pr
pr2←{k←⌊32÷2⍟b←⊃bas ⍵ ⋄ ((k≥pla-top)×⊃∘bas⊥k↑⍨∘-⍨⊢)⍵}
pr3←{0∊⍴⍵:⍵ ⋄ b←⊃bas ⍵ ⋄ ⍵/⍨(⍺=pr2⍵)∨0≠(⍺|+∘(b∘×))⌿⊖sg0⍵}
mr0←{0∊⍴⍵:⍵ ⋄ z←⍵ sub ⍵ big 1
 ⍵/⍨((0∘≠∧⍺∘≥)pr2⍵)∨(z rav ⍵ big 1)eps⍨(flo di2 z)(⍵ mex)⍵ big ⍺}
mr1←{0∊⍴⍵:⍵ ⋄ ⊃mr0/(2+⊃?/0 ¯2+⍺),⊂⍵}
mrp←{⍺←(2∘⍟,⍨∘⌈⍨2××⍨)(⌈/∘,2∘⍟∘⊃∘bas×pla-top)⍵
 ⍺mr1⊃pr3/(⌽pr1 1⊃⍺),⊂1(⊢(/⍨∘~)big⍨eps⍨⊢)⍵}
rp0←mrp∘(⊃∘bas⍪sg0↑⍨∘-2×1+pla)(⊢(/⍨)leq )∘rol
rp1←{~0∊⍴⍺⍺: (pla⍵)pla 0sqd⍺⍺ ⋄ ⍺←⊢ ⋄ ⍺((⍺ rp0 ⍵)∇∇)⍵}
rpr←{⍺←⊢ ⋄ b←⊃bas ⍵ ⋄ n←(⌈2⍟b)×pla ⍵ ⋄ ⍺(⍬ rp1)⊢n rho ⍵}
sp0←{0∊⍴⍵:⍵ ⋄ b←⊃bas⍵ ⋄ ⍵/⍨∧⌿(⍺=pr2⍵)∨0≠(⍺|+∘(b∘×))⌿⊖sg0⍵}
sp1←{0∊⍴⍵:⍵ ⋄ 0=2|⊃bas⍵:⍵/⍨∧⌿(2=pr2⍵)∨2|0⌷¯1↑⍵ ⋄ 2 sp0⍵}
sp2←{0∊⍴⍵:⍵ ⋄ z←⍵ sub ⍵ big 1
 ⍵/⍨∧⌿(⍺≥pr2⍵)∨(z rav ⍵ big 1)eps rop 1⍨(flo di2 z)(⍵ mex)⍵ big ⍺}
sp3←{0∊⍴⍵:⍵ ⋄ 0sqd⊃sp2/(2+⊃?/0 ¯2+⍺),⊂⍵}
sgp←{⍺←(2∘⍟,⍨∘⌈⍨2××⍨)(⌈/∘,1+2∘⍟∘⊃∘bas×pla-top)⍵
 ⍺sp3⊃sp0/(⌽1↓pr1 1⊃⍺),⊂sp1⊢1(⊢(/⍨)∘(∧⌿~)big⍨eps⍨⊢)(⊢,[.5]big∘1 add 3∘↑⍪2×3∘↓)⍵}
sp4←sgp∘(⊃∘bas⍪sg0↑⍨∘-2×1+pla)(⊢(/⍨)leq )∘rol
sp5←{~0∊⍴⍺⍺: (pla⍵)pla 0sqd⍺⍺ ⋄ ⍺←⊢ ⋄ ⍺((⍺ sp4 ⍵)∇∇)⍵}
rsg←{⍺←⊢ ⋄ b←⊃bas ⍵ ⋄ n←(⌈2⍟b)×pla ⍵ ⋄ ⍺(⍬ sp5)⊢n rho ⍵}
