⎕IO ⎕ML ⎕WX←0 1 3

Scn←{2>≢⍵:⍵ ⋄ ↑⊖⊃⍺⍺{⍵,⍨⊂(⊃⍵)⍺⍺ ⍺}⌿⊖(⊂∘⊂∘⊃,1↓⊢)⊂⍤¯1⊢⍵}
hex←{⍺←⌈/⌈2⍟1+,⍵
 '0123456789abcdef'[2⊥⍉(⊢((4,⍨⊢)⍴(4×⊢)↑⊣)∘⌈÷∘4∘≢),⍉(⍺⍴2)⊤,⍵]}
h←{⍵⍴⍨(2⍴⍨2⍟≢⍵),1↓⍴⍵}⋄ u←{×\1,1↓(⍵÷2)⍴*○0J2÷⍵}
fft←{(⍴⍵)⍴(h u≢⍵){(⊣/⍺)∇⍣(×m)⊢(+⌿⍵),[m-0.5]⍺×[⍳m←≢⍴⍺]-⌿⍵}h⍵}
ift←{(≢⍵)÷⍨(⍴⍵)⍴(h+u≢⍵){(⊣/⍺)∇⍣(×m)⊢(+⌿⍵),[m-0.5]⍺×[⍳m←≢⍴⍺]-⌿⍵}h⍵}
mta←⊣⍪(1↓⊢)
bas←(2*16)|0∘⌷
ful←bas mta⊢
par←((2*16)(⊣+|)0⌷⊢)mta⊢
pla←(0⌷⊢)⍪(-⊣)↑1↓⊢
rnd←(0⌷⊢)⍪(1-∘≢⊢)↑(-⊣)↓1↓⊢
⍝cry←{b←(2*16)|m←⊃⍵ ⋄ m<2*16:m mta b|⊖+∘⌊∘(÷∘b)⍨Scn⊖⍵
⍝ m mta(b∘|+0⍪⍨1↓∘⌊÷∘b)⍣(⌈32÷2⍟b)⊢⍵}
cry←{k←⌈32÷2⍟b←(2*16)|m←⊃⍵ ⋄ f←(k⍴b)∘(+⌿(⌽⍳k)↓⍤¯1⊤)
 m mta(b⊤⊖∘(+∘(0⌷(2⍴b)∘⊤)⍨Scn)∘⊖)⍣(m=b)⊢f⍣(2+(b<10)+(b=2))⊢(⊖+⍀⊖(b-1)×f-0⌊⍵)+f 0⌈⍵}
add←cry⌊∘⊃⍨∘⊃mta+⍤¯1
sub←cry⌊∘⊃⍨∘⊃mta-⍤¯1
mul←{cry((⊃⍺)⌈⊃⍵)⍪⌊0.5+9○(1-≢⍵)↑N↑ift⊃×⍤¯1/fft¨(-2*⌈2⍟N←¯3+(≢⍺)+≢⍵)↑¨1↓¨⍺ ⍵}
cat←⍪⍤ ¯1
rav←,⍤ ¯1
trn←⍉⍤ 15 ¯1
rot←⌽⍤15 ¯1
rof←⊖⍤15 ¯1
pic←⊃⍤ ¯1
sqd←⌷⍤15 ¯1
eql←∧⌿0=1↓sub∘ful⍨∘ful
neq←∨⌿0≠1↓sub∘ful⍨∘ful
leq←0=1⌷sub∘ful⍨∘ful
geq←leq⍨
gth←0≠1⌷sub∘ful⍨∘ful
lth←gth⍨
max←{((0⌷⍺)mta⍺×⍤15 ¯1⍨~z)add(0⌷⍵)mta(z←⍺ leq ⍵)×⍤15 ¯1⊢⍵}
min←{((0⌷⍺)mta⍺×⍤15 ¯1⍨~z)add(0⌷⍵)mta(z←⍺ geq ⍵)×⍤15 ¯1⊢⍵}
abs←max∘sub⍨
eps←{⍺←⊢ ⋄ 1≡⍺ 1:∊⍤¯1⊢⍵ ⋄ ⍉∨/(⍉⍺)eql⍤1⍤1 15⍉⍵}
ind←{1(⍳⍨)⍤1⍉(⍺⍉⍨¯1⌽⍳⍴⍴⍺){∧/,(⍉⍺)eql(⍉⍵)}⍤(¯1+⍴⍴⍺)⍤((¯1+⍴⍴⍺),15)⊢(⍵⍉⍨¯1⌽⍳⍴⍴⍵)}
rol←{z←?(⍴⍵)⍴(2*8)|m←0⌷⍵ ⋄ (0mta⍵)≡0⍴⍨⍴⍵:m mta z ⋄ n↑⊃mul/m mta¨(1-2×n←⊃⍴⍵)↑¨1↓¨z ⍵}

dot←{⍺⍺rdf(0 1,⍨(2+⌽⍳¯2+≢⍴⍺),(≢⍴⍺)+⍳¯2+≢⍴⍵)⍉(⍺⍉⍨1⌽⌽⍳≢⍴⍺)⍵⍵⍤2⍤2 15⊢⍵⍉⍨(¯2+≢⍴⍵)⌽⍳≢⍴⍵}
out←{(0,⍨(⌽1+⍳¯1+≢⍴⍺),(≢⍴⍺)+⌽⍳¯1+≢⍴⍵)⍉(⍉⍺)⍺⍺⍤1⍤1 15⍉⍵}
rdf←{cry(⌊⌿0⌷⍵)mta⊃⍺⍺/⊂[1~⍨⍳≢⍴⍵]par ⍵}
scf←{cry(0⌷⍵)mta(1 0,2↓⍳≢⍴⍵)⍉↑⍺⍺\⊂[1~⍨⍳≢⍴⍵]par ⍵}
pop←{⍺←⊢ ⋄ 1≡⍺ 1:cry (0⌷⍵) mta⍺⍺⍣⍵⍵⊢par ⍵ ⋄ cry (0⌷⍵)mta⊃⍺⍺⍣⍵⍵/par¨⍺ ⍵}

spl←{⍉⊂⍤2⊢⍵⍉⍨1⌽⌽⍳⍴⍴⍵}
mix←{⍵⍉⍨1⌽⌽⍳⍴⍴⍵}↑∘⍉
