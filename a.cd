⎕IO ⎕ML ⎕WX←0 1 3

Scn←{2>≢⍵:⍵ ⋄ ↑⊖⊃⍺⍺{⍵,⍨⊂(⊃⍵)⍺⍺ ⍺}⌿⊖(⊂∘⊂∘⊃,1↓⊢)⊂⍤¯1⊢⍵}
hex←{⍺←⌈/⌈2⍟1+,⍵ ⋄ '0123456789abcdef'[2⊥⍉(⊢((4,⍨⊢)⍴(4×⊢)↑⊣)∘⌈÷∘4∘≢),⍉(⍺⍴2)⊤,⍵]}
fft←{q←1+2⍟n←2÷⍨≢⍵ ⋄ v←1↓⍴⍵ ⋄ u←×\1,1↓⊢⍴∘*∘○0J1∘÷
 f←⊣((+⌿⊢),[.5](u⊣)×[0](-⌿⊢))⊢⍴⍨2,v,⍨⊣,n÷⊣ ⋄ (⍴⍵)⍴⊃f/(2*⍳q),⊂⍵}
ift←{q←1+2⍟n←2÷⍨≢⍵ ⋄ v←1↓⍴⍵ ⋄ u←×\1,1↓⊢⍴∘*∘○0J¯1∘÷
 f←⊣((+⌿⊢),[.5](u⊣)×[0](-⌿⊢))⊢⍴⍨2,v,⍨⊣,n÷⊣ ⋄ (≢⍵)÷⍨(⍴⍵)⍴⊃f/(2*⍳q),⊂⍵}
mta←(0∘⌷⊣)⍪(1∘⌷⊣)⍪(2∘↓⊢)
bas←(2*16)|0∘⌷
ful←bas⍪1∘↓
par←1∘↓⍪⍨(2*16)(|+×∘×)0∘⌷
isr←=∘|⍨⊃
r2c←(⌽1 0J¯1 ¯1 0J1⍴⍨≢)×[0]⊢
c2r←(⌽1 0J1 ¯1 0J¯1⍴⍨≢)×[0]⊢
sg0←{b←(2*16)|⊃⍵ ⋄ (~isr ⍵)∨b=⊃⍵:⍵ ⋄ z←∧⍀2↓⍵=b-1
 (2↑⍵)⍪(2↓⍵)×~(1⍪¯1↓z)×⍤¯1 15⊢b=0⌷(+⌿z)⊖2↓⍵}
zer←{isr ⍵:∧⌿0=2↓sg0 ⍵ ⋄ b←⊃bas⍵ ⋄ 0=(⊢+(b∘×⊣))⌿2↓⍵}
neg←.5∘×∘bas≤2⌷sg0
cry←{r←isr ⍵ ⋄ k←⌈(2*.5×~r)×32÷2⍟|b←(2*16)|⊃⍵ ⋄ f←(k⍴b)∘(+⌿(⌽⍳k)↓⍤¯1⊤)
 r:(2↑⍵)mta(b⊤⊖∘(+∘(0⌷(2⍴b)∘⊤)⍨Scn)∘⊖)⍣(b=⊃⍵)⊢f⍣(2+10 3+.>b)⊢(((⊖+⍀)∘⊖(b-1)×(f∘-0∘⌊))+(f 0∘⌈))⊢⍵
 (2↑⍵)mta(b⊤⊖∘(+∘(b⊥¯1↓(b⍴⍨4+8 3+.>|b)∘⊤)⍨Scn)∘⊖)⍣(b=⊃⍵)⊢f⍣(1+14>|b)⊢⍵}
ima←{1=isr ⍵: (2↑⍵)mta 0⍴⍨⍴⍵ ⋄ cry(|2↑⍵)mta 11○c2r⍵}
rea←{1=isr ⍵: ⍵ ⋄ cry(|2↑⍵)mta 9○c2r⍵}
top←{isr⍵:((+⌿∧⍀)3↓sg0=-∘neg⊤⍨≢⍴bas)⍵ ⋄ ((+⌿∧⍀)0=3∘↓)⍵}
ovr←top+1∘⌷
mov←{k←⍺-1⌷⍵ ⋄ isr ⍵:(0⌷⍵)⍪⍺⍪(2-≢⍵)↓k⊖(2∘↓⍪2↓(-neg∧⍺<1∘⌷)⊤⍨≢⍴bas)⍵
 (0⌷⍵)⍪⍺⍪(2-≢⍵)↑k⊖(2×2-≢⍵)↑2↓⍵}
com←{((0J1*(isr ⍺)⍲isr ⍵)×(|⊃⍺)⌊|⊃⍵),[¯0.5]((ovr ⍺)⌊ovr ⍵)⌊(1⌷⍺)⌈1⌷⍵}
ch0←{r2c⍣((isr⍺)<isr⍵)⊢⍵mov⍨(⍴1⌷⍵)⍴1⌷⍺}
cnj←{isr ⍵: ⍵ ⋄ (cry 2∘↑mta(⌽1 ¯1⍴⍨≢)×[0]+)⍵}
add←{m←⍺ com ⍵ ⋄ cry m mta(m ch0 ⍺)+⍤¯1⊢m ch0 ⍵}
sub←{⍺←⊢ ⋄ 1≡⍺1: cry(2∘↑mta-)⍵ ⋄ m←⍺ com ⍵ ⋄ cry m mta(m ch0 ⍺)-⍤¯1⊢m ch0⍵}
mul←{m←((0J1*(isr ⍺)⍲isr ⍵)×(|⊃⍺)⌊|⊃⍵),[¯0.5](1⌷⍺)+1⌷⍵
 n←-2*⌈2⍟¯5+2×≢⍵ ⋄ k←0⌊(top⍺)+(top⍵)+3-≢⍵
 (0⌷m)⍪(k+1⌷m)⍪(2-≢⍵)↑k⊖2↓cry m⍪¯1⊖⌊0.1J0.1+ift⊃×⍤¯1/fft¨n↑¨2↓¨⍺⍵}
cat←⍪⍤¯1
rav←,⍤¯1
trn←⍉⍤15 ¯1
rot←⌽⍤15 ¯1
rof←⊖⍤15 ¯1
pic←⊃⍤¯1
sqd←⌷⍤15 ¯1
eql←zer sub
neq←~eql
lth←neg sub
gth←lth⍨
geq←~lth
leq←~gth
flo←0∘mov
cel←sub flo∘sub
min←{((2↑⍺)mta⍺×⍤15 ¯1⍨~z)add(2↑⍵)mta(z←⍺ geq ⍵)×⍤15 ¯1⊢⍵}
max←{((2↑⍺)mta⍺×⍤15 ¯1⍨~z)add(2↑⍵)mta(z←⍺ leq ⍵)×⍤15 ¯1⊢⍵}
abs←max∘sub⍨
rho←{⍺←0∘⌷ ⋄ ⍺⍴⍤15 ¯1⊢⍵}
eps←{⍺←⊢ ⋄ 1≡⍺1:∊⍤¯1⊢⍵ ⋄ ⍉∨/(⍉⍺)eql⍤1⍤1 15⍉⍵}
ind←{1(⍳⍨)⍤1⍉(⍺⍉⍨¯1⌽⍳⍴⍴⍺){∧/,(⍉⍺)eql(⍉⍵)}⍤(¯1+⍴⍴⍺)⍤((¯1+⍴⍴⍺),15)⊢(⍵⍉⍨¯1⌽⍳⍴⍴⍵)}
rol←{z←(0∘⌷⍪¯3∘+∘≢⍪0⍪3↓⍴(?⍴)bas)⍵ ⋄ (2↓⍵)≡2↓0⍴⍨⍴⍵:z ⋄ flo z mul ⍵}
tke←(⊃0⌷⊢)⍪(⊃1⌷⊢)⍪(2∘↓↑⍤15 ¯1)
drp←(⊃0⌷⊢)⍪(⊃1⌷⊢)⍪(2∘↓↓⍤15 ¯1)
spl←{⍉⊂⍤2⊢⍵⍉⍨1⌽⌽⍳⍴⍴⍵}
mix←{l←×/r←⌽⌈⌿↑,⌽∘rho¨⍵ ⋄ {⍵⍉⍨(¯1∘↑,⍨1⌽¯1∘↓)⍳⍴⍴⍵}↑(⊂r)rho¨l tke¨rav¨⍵}

red←{cry(⌊/2↑⍵)mta⊃⍺⍺/⊂[¯1↓⍳≢⍴⍵]par⍵}
scn←{cry(2↑⍵)mta(¯1⌽⍳≢⍴⍵)⍉↑⍺⍺\⊂[¯1↓⍳≢⍴⍵]par⍵}
rdf←{cry(⌊/[1]2↑⍵)mta⊃⍺⍺/⊂[1~⍨⍳≢⍴⍵]par⍵}
scf←{cry(2↑⍵)mta(1 0,2↓⍳≢⍴⍵)⍉↑⍺⍺\⊂[1~⍨⍳≢⍴⍵]par⍵}
dot←{⍺⍺rdf(0 1,⍨(2+⌽⍳¯2+≢⍴⍺),(≢⍴⍺)+⍳¯2+≢⍴⍵)⍉(⍺⍉⍨1⌽⌽⍳≢⍴⍺)⍵⍵⍤2⍤2 15⊢⍵⍉⍨(¯2+≢⍴⍵)⌽⍳≢⍴⍵}
out←{(0,⍨(⌽1+⍳¯1+≢⍴⍺),(≢⍴⍺)+⌽⍳¯1+≢⍴⍵)⍉(⍉⍺)⍺⍺⍤1⍤1 15⍉⍵}
pop←{⍺←⊢ ⋄ 1≡⍺1:cry(2↑⍵)mta⍺⍺⍣⍵⍵⊢par⍵ ⋄ cry(2↑⍵)mta⊃⍺⍺⍣⍵⍵/par¨⍺⍵}


