⎕IO ⎕ML ⎕WX←0 1 3

Scn←{2>≢⍵:⍵ ⋄ ↑⊖⊃⍺⍺{⍵,⍨⊂(⊃⍵)⍺⍺ ⍺}⌿⊖(⊂∘⊂∘⊃,1↓⊢)⊂⍤¯1⊢⍵}
hex←{⍺←⌈/⌈2⍟1+,⍵ ⋄ '0123456789abcdef'[2⊥⍉(⊢((4,⍨⊢)⍴(4×⊢)↑⊣)∘⌈÷∘4∘≢),⍉(⍺⍴2)⊤,⍵]}
fft←{q←1+2⍟n←2÷⍨≢⍵ ⋄ v←1↓⍴⍵ ⋄ u←×\1,1↓⊢⍴∘*∘○0J1∘÷
 f←⊣((+⌿⊢),[.5](u⊣)×[0](-⌿⊢))⊢⍴⍨2,v,⍨⊣,n÷⊣ ⋄ (⍴⍵)⍴⊃f/(2*⍳q),⊂⍵}
ift←{q←1+2⍟n←2÷⍨≢⍵ ⋄ v←1↓⍴⍵ ⋄ u←×\1,1↓⊢⍴∘*∘○0J¯1∘÷
 f←⊣((+⌿⊢),[.5](u⊣)×[0](-⌿⊢))⊢⍴⍨2,v,⍨⊣,n÷⊣ ⋄ (≢⍵)÷⍨(⍴⍵)⍴⊃f/(2*⍳q),⊂⍵}
mta←(0∘⌷⊣)⍪(1∘⌷⊣)⍪(2∘⌷⊣)⍪(3∘↓⊢)
bas←(2*16)|0∘⌷
ful←bas⍪1∘↓
par←1∘↓⍪⍨(2*16)(|+×∘×)0∘⌷
isr←=∘|⍨⊃
neg←{~isr⍵:⎕EM 11 ⋄ 2∘⌷⍵}
zer←{isr⍵:∧⌿0=2↓⍵ ⋄ b←⊃bas⍵ ⋄ 0=(⊢+(b∘×⊣))⌿3↓⍵}
sg0←{b←(2*16)|⊃⍵ ⋄ b=⊃⍵:⍵ ⋄ z←∧⍀3↓⍵=b-1
 z←(1⍪¯1↓z)⍲⍤¯1 15⊢b=0⌷(+⌿z)⊖3↓⍵ ⋄ (2↑⍵)⍪((2⌷⍵)=∧⌿z)⍪z×3↓⍵}
cry←{r←isr⍵ ⋄ k←⌈(2*.5×~r)×32÷2⍟|b←(2*16)|⊃⍵ ⋄ f←(k⍴b)∘(+⌿(⌽⍳k)↓⍤¯1⊤) 
 g←((⊖+⍀)∘⊖(b-1)×(f∘-0∘⌊))+(f 0∘⌈) ⋄ h←(2|2∘⌷)⍪3∘↓
 r:sg0(2↑⍵)⍪h(b⊤⊖∘(+∘(0⌷(2⍴b)∘⊤)⍨Scn)∘⊖)⍣(b=⊃⍵)⊢f⍣(2+10 3+.>b)⊢g ⍵
 (3↑⍵)⍪3↓(b⊤⊖∘(+∘(b⊥¯1↓(b⍴⍨4+8 3+.>|b)∘⊤)⍨Scn)∘⊖)⍣(b=⊃⍵)⊢f⍣(1+14>|b)⊢⍵}
top←{isr⍵:((+⌿∧⍀)3↓⊢=-∘neg⊤⍨≢⍴bas)⍵ ⋄ ((+⌿∧⍀)0=3∘↓)⍵}
ovr←top+1∘⌷
pla←{k←0⌊(n←⍺+3-≢⍵)+top⍵ ⋄ m←(0⌷⍵)⍪(k+1⌷⍵),[¯.5]2⌷⍵
 isr ⍵: m⍪((-∘neg⊤⍨(0⌈n)⍴⊃∘bas)⍵)⍪(-⍺⌊¯3+≢⍵)↑k⊖3↓⍵ ⋄ m⍪(-⍺)↑k⊖3↓⍵}
mov←{k←⍺-1⌷⍵ ⋄ isr ⍵:(0⌷⍵)⍪⍺⍪(2⌷⍵)⍪(3-≢⍵)↓k⊖(3∘↓⍪3↓(-neg∧⍺<1∘⌷)⊤⍨≢⍴bas)⍵
 (0⌷⍵)⍪⍺⍪(2⌷⍵)⍪(3-≢⍵)↑k⊖(2×3-≢⍵)↑3↓⍵}
com←{((0J1*(isr⍺)⍲isr⍵)×(|⊃⍺)⌊|⊃⍵)⍪(¯3+((ovr⍺)⌊ovr⍵)⌊(1⌷⍺)⌈1⌷⍵),[¯.5](2⌷⍺)≠2⌷⍵}
r2c←(¯3+≢)pla∘cry(0J1×0∘⌷)⍪1∘⌷⍪0⍪-∘≢↑3∘↓×[0]1 0J¯1 ¯1 0J1(⌽⍴)⍨¯3+≢
ch0←{(1⌷⍺)mov (⍵↑⍨3+≢⍵)⍴⍤15 ¯1⍣(⊣≢(⍴1⌷⊢))⍨⍴1⌷⍺}
ch1←{r←3⌊top⍵ ⋄ (0⌷⍵)⍪(r+1⌷⍵)⍪(2⌷⍵)⍪¯3↓r⊖3↓⍵}
cr1←¯3∘+∘≢pla∘cry⊢pla⍨≢-2×isr
cnj←{isr ⍵: ⍵ ⋄ (cry 3∘↑mta(⌽1 ¯1⍴⍨≢)×[0]+)⍵}
add←{a←r2c⍣((isr⍵)<isr⍺)⊢⍺ ⋄ w←r2c⍣((isr⍺)<isr⍵)⊢⍵ ⋄
 m←a com w ⋄ ch1 cry m mta(m ch0 a)+m ch0 w}
sub←{⍺←⊢ ⋄ 1≡⍺1: cry(3∘↑mta-)⍵ ⋄ a←r2c⍣((isr⍵)<isr⍺)⊢⍺ ⋄ w←r2c⍣((isr⍺)<isr⍵)⊢⍵
 m←a com w ⋄ ch1 cry m mta(m ch0 a)-m ch0 w}
sg1←3∘↓⍪⍨2∘↑⍪2∘⌷∧0(∨⌿≠)3∘↓
⍝mul←{(≢⍺)≠≢⍵:⎕EM 5 ⋄ k←0⌊(top ⍺)+(top ⍵)+3-≢⍵
⍝ m←((0J1*(isr⍺)⍲isr⍵)×(|⊃⍺)⌊|⊃⍵)⍪((1⌷⍺)+1⌷⍵),[¯.5](2⌷⍺)≠2⌷⍵
⍝ sg1(0⌷m)⍪(k+1⌷m)⍪(2⌷m)⍪(3-≢⍵)↑k⊖3↓cry m⍪¯1⊖⌊0.1J0.1+ift⊃×⍤¯1/fft¨(-2*⌈2⍟¯7+2×≢⍵)↑¨3↓¨⍺⍵}
mul←{(≢⍺)≠≢⍵:⎕EM 5 ⋄ k←0⌊(top ⍺)+(top ⍵)+3-≢⍵
 m←((0J1*(isr⍺)⍲isr⍵)×(|⊃⍺)⌊|⊃⍵)⍪((1⌷⍺)+1⌷⍵),[¯.5](2⌷⍺)≠2⌷⍵
 sg1(0⌷m)⍪(k+1⌷m)⍪(2⌷m)⍪(3-≢⍵)↑k⊖3↓cry m⍪¯1⊖⌊0.1J0.1+ift⊃×⍤¯1/fft¨(-2*⌈2⍟¯7+2×≢⍵)↑¨(4∘↓⍪⍨3∘⌷-bas×2∘⌷)¨⍺⍵}
rea←{isr ⍵: ⍵ ⋄ cry(|3↑⍵)mta 9○(⊢×[0]1 0J1 ¯1 0J¯1(⌽⍴)⍨≢) ⍵}
ima←{isr ⍵: (3↑1↑⍵)mta 0⍴⍨⍴⍵ ⋄ cry(|3↑⍵)mta 11○(⊢×[0]1 0J1 ¯1 0J¯1(⌽⍴)⍨≢) ⍵}
cat←⍪⍤¯1
rav←,⍤¯1
trn←⍉⍤15 ¯1
rot←⌽⍤15 ¯1
rof←⊖⍤15 ¯1
pic←⊃⍤¯1
sqd←⌷⍤15 ¯1
eql←zer sub
neq←~eql
lth←neg sub
gth←lth⍨
geq←~lth
leq←~gth
flo←⊢mov⍨0⌊1∘⌷
cel←sub flo∘sub
min←{cr1((⊃⍺)⌊⊃⍵)⍪1↓(⍺×⍤15 ¯1⍨~z)+(z←⍺ geq ⍵)×⍤15 ¯1⊢⍵}
max←{cr1((⊃⍺)⌊⊃⍵)⍪1↓(⍺×⍤15 ¯1⍨~z)+(z←⍺ leq ⍵)×⍤15 ¯1⊢⍵}
abs←max∘sub⍨
rho←{⍺←0∘⌷ ⋄ ⍺⍴⍤15 ¯1⊢⍵}
eps←{⍺←⊢ ⋄ 1≡⍺1:∊⍤¯1⊢⍵ ⋄ ⍉∨/(⍉⍺)eql⍤1⍤1 15⍉⍵}
ind←{1(⍳⍨)⍤1⍉(⍺⍉⍨¯1⌽⍳⍴⍴⍺){∧/,(⍉⍺)eql(⍉⍵)}⍤(¯1+⍴⍴⍺)⍤((¯1+⍴⍴⍺),15)⊢(⍵⍉⍨¯1⌽⍳⍴⍴⍵)}
rol←{(~isr ⍵)∨∨/,(2⌷⍵),0≠1⌷⍵:⎕EM 11 ⋄ z←(0∘⌷⍪¯3∘+∘≢⍪0⍪3↓⍴(?⍴)bas)⍵
 (3↓⍵)≡3↓0⍴⍨⍴⍵:z ⋄ (0⌷⍵)⍪1↓(z((⊣×⍤¯1 15zer)+(flo mul))⍵)}
tke←(⊃0⌷⊢)⍪1↓↑⍤15 ¯1
drp←(⊃0⌷⊢)⍪1↓↓⍤15 ¯1
spl←(⍉⊂⍤2)⊢⍉⍨1⌽⌽∘⍳∘⍴∘⍴
mix←{r←⌽⌈⌿↑,⌽∘rho¨⍵ ⋄ n←1+⍴⍴⍵
 (⊢⍴⍨1↓⍴)⍣(⍬≡⍴⍵)(⊢⍉⍨(1⌽⍳n)⍪n↓⍳∘⍴∘⍴)↑(⊂r)tke¨⍵rho⍨¨(⊢,¨⍨1⍴¨⍨(⍴r)-⍴¨)rho¨⍵}

red←{cr1((0J1*~isr⍵)×⌊/|0⌷⍵)⍪1↓⊃⍺⍺/⊂[¯1↓⍳≢⍴⍵]par⍵}
scn←{cr1(0⌷⍵)⍪1↓(¯1⌽⍳≢⍴⍵)⍉↑⍺⍺\⊂[¯1↓⍳≢⍴⍵]par⍵}
rdf←{cr1((0J1*~isr⍵)×⌊⌿|0⌷⍵)⍪1↓⊃⍺⍺/⊂[1~⍨⍳≢⍴⍵]par⍵}
scf←{cr1(0⌷⍵)⍪1↓(1 0,2↓⍳≢⍴⍵)⍉↑⍺⍺\⊂[1~⍨⍳≢⍴⍵]par⍵}
dot←{⍺⍺rdf(0 1,⍨(2+⌽⍳¯2+≢⍴⍺),(≢⍴⍺)+⍳¯2+≢⍴⍵)⍉(⍺⍉⍨1⌽⌽⍳≢⍴⍺)⍵⍵⍤2⍤2 15⊢⍵⍉⍨(¯2+≢⍴⍵)⌽⍳≢⍴⍵}
out←{(0,⍨(⌽1+⍳¯1+≢⍴⍺),(≢⍴⍺)+⌽⍳¯1+≢⍴⍵)⍉(⍉⍺)⍺⍺⍤1⍤1 15⍉⍵}
pop←{⍺←⊢ ⋄ 1≡⍺1:cr1(0⌷⍵)⍪1↓⍺⍺⍣⍵⍵⊢par⍵ ⋄ cr1(0⌷⍵)⍪1↓⊃⍺⍺⍣⍵⍵/par¨⍺⍵}
