⎕IO ⎕ML ⎕WX←0 1 3

Scn←{2>≢⍵:⍵ ⋄ ↑⊖⊃⍺⍺{⍵,⍨⊂(⊃⍵)⍺⍺ ⍺}⌿⊖(⊂∘⊂∘⊃,1↓⊢)⊂⍤¯1⊢⍵}
hex←{⍺←⌈/⌈2⍟1+,⍵ ⋄ '0123456789abcdef'[2⊥⍉(⊢((4,⍨⊢)⍴(4×⊢)↑⊣)∘⌈÷∘4∘≢),⍉(⍺⍴2)⊤,⍵]}
fft←{q←1+2⍟n←2÷⍨≢⍵ ⋄ v←1↓⍴⍵ ⋄ u←×\1,1↓⊢⍴∘*∘○0J1∘÷
 f←⊣((+⌿⊢),[.5](u⊣)×[0](-⌿⊢))⊢⍴⍨2,v,⍨⊣,n÷⊣ ⋄ (⍴⍵)⍴⊃f/(2*⍳q),⊂⍵}
ift←{q←1+2⍟n←2÷⍨≢⍵ ⋄ v←1↓⍴⍵ ⋄ u←×\1,1↓⊢⍴∘*∘○0J¯1∘÷
 f←⊣((+⌿⊢),[.5](u⊣)×[0](-⌿⊢))⊢⍴⍨2,v,⍨⊣,n÷⊣ ⋄ (≢⍵)÷⍨(⍴⍵)⍴⊃f/(2*⍳q),⊂⍵}
mta←(0∘⌷⊣)⍪(1∘⌷⊣)⍪(2∘↓⊢)
bas←(2*16)|0∘⌷
ful←⊢mta⍨bas,[¯.5]1∘⌷
par←⊢mta⍨1∘⌷,[¯.5]⍨(2*16)(⊣+|)0∘⌷
isr←=∘|⍨⊃
com←(0J1 1*∘~∧∘isr⍨∘isr)×[0]⌊⍤¯1∘(|2∘↑)⍨∘(|2∘↑)
sg0←{b←(2*16)|⊃⍵ ⋄ (~isr⍵)∨b=⊃⍵:⍵
 (2↑⍵)⍪(2↓⍵)×~(∧⍀2↓⍵∊b,b-1)×⍤¯1 15⊢⍉b=(⍉(¯3+≢⍵)⌊+⌿∧⍀2↓⍵=b-1)⌷⍤0 1⊢⍉2↓⍵}
zer←∧⌿0=2↓sg0
neg←(2<≢-1∘⌷)∧2∘⌷∘sg0≥.5×bas
pla←{¯2=⍺-≢⍵:⍵ ⋄ isr ⍵:(2↑⍵)⍪(⍉(0⌈⍺+2-≢⍵)⍴⍤0⍉(neg×¯1+bas)⍵)⍪⍵↑⍨-⍺⌊¯2+≢⍵
 (2↑⍵)⍪(-⍺)↑2↓⍵}          
mov←{0=n←⍺-⊃1⌷⍵:⍵ ⋄ isr ⍵:(0⌷⍵)⍪⍺⍪(¯2+≢⍵)↑(⍉(0⌈-n)⍴⍤0⍉(neg×¯1+bas)⍵)⍪n↓2↓⍵
 (0⌷⍵)⍪⍺⍪(0⌊n)⊖(¯2+≢⍵)↑n↓2↓⍵}
cry←{k←⌈32÷2⍟|b←(2*16)|⊃⍵ ⋄ c←isr ⍵ ⋄ f←(k⍴b)∘(+⌿(⌽⍳k)↓⍤¯1⊤)
 (2↑⍵)mta(b⊤⊖∘(+∘(0⌷(2⍴b)∘⊤)⍨Scn)∘⊖)⍣(c∧b=⊃⍵)⊢f⍣(2+(10>|b)+(2=|b)+~c)⊢(((⊖+⍀)∘⊖(b-1)×(f∘-0∘⌊))+(f 0∘⌈))⍣c⊢⍵}
lhs←{(⌽×\1,0J¯1⍴⍨¯1+≢⍵)×[0]⍣((isr⍺)>isr ⍵)⊢⍺mov⍨⊃(1⌷⍺)⌊(1⌷⍵)}
rhs←{(⌽×\1,0J¯1⍴⍨¯1+≢⍵)×[0]⍣((isr⍺)<isr ⍵)⊢⍵mov⍨⊃(1⌷⍺)⌊(1⌷⍵)}
add←cry com mta lhs+⍤¯1rhs
sub←{⍺←⊢ ⋄ 1≡⍺1: cry(2∘↑mta-)⍵ ⋄ ⍺(cry com mta lhs-⍤¯1rhs)⍵}
mul←{m←((0J1*∘~(isr⍺)∧isr⍵)×(|⊃⍺)⌊|⊃⍵),[¯.5]1+(1⌷⍺)+1⌷⍵  ⋄ k←⊃(1⌷⍺)⌊1⌷⍵ 
 (¯2+≢⍵)pla k mov cry m⍪⌊.1J.1+ift⊃×⍤¯1/fft¨(¯2*⌈2⍟¯5+(≢⍺)+≢⍵)↑¨2↓¨⍺⍵}
cat←⍪⍤¯1
rav←,⍤¯1
trn←⍉⍤15 ¯1
rot←⌽⍤15 ¯1
rof←⊖⍤15 ¯1
pic←⊃⍤¯1
sqd←⌷⍤15 ¯1
tke←(⊃0⌷⊢)⍪(⊃1⌷⊢)⍪(2∘↓↑⍤15 ¯1)  
drp←(⊃0⌷⊢)⍪(⊃1⌷⊢)⍪(2∘↓↓⍤15 ¯1)
eql←zer sub
neq←~eql
lth←neg sub
gth←lth⍨
geq←~lth
leq←~gth
max←{((2↑⍺)mta⍺×⍤15 ¯1⍨~z)add(2↑⍵)mta(z←⍺ leq ⍵)×⍤15 ¯1⊢⍵}
min←{((2↑⍺)mta⍺×⍤15 ¯1⍨~z)add(2↑⍵)mta(z←⍺ geq ⍵)×⍤15 ¯1⊢⍵}
abs←max∘sub⍨
eps←{⍺←⊢ ⋄ 1≡⍺1:∊⍤¯1⊢⍵ ⋄ ⍉∨/(⍉⍺)eql⍤1⍤1 15⍉⍵}
ind←{1(⍳⍨)⍤1⍉(⍺⍉⍨¯1⌽⍳⍴⍴⍺){∧/,(⍉⍺)eql(⍉⍵)}⍤(¯1+⍴⍴⍺)⍤((¯1+⍴⍴⍺),15)⊢(⍵⍉⍨¯1⌽⍳⍴⍴⍵)}
rol←{z←((0⌷⍵),[¯.5]¯2+≢⍵)mta?(⍴⍵)⍴bas⍵ ⋄ (2↓⍵)≡2↓0⍴⍨⍴⍵:z ⋄ z mul ⍵}

red←{cry(⌊/2↑⍵)mta⊃⍺⍺/⊂[¯1↓⍳≢⍴⍵]par⍵}
scn←{cry(2↑⍵)mta(¯1⌽⍳≢⍴⍵)⍉↑⍺⍺\⊂[¯1↓⍳≢⍴⍵]par⍵}
rdf←{cry(⌊/[1]2↑⍵)mta⊃⍺⍺/⊂[1~⍨⍳≢⍴⍵]par⍵}
scf←{cry(2↑⍵)mta(1 0,2↓⍳≢⍴⍵)⍉↑⍺⍺\⊂[1~⍨⍳≢⍴⍵]par⍵}
dot←{⍺⍺rdf(0 1,⍨(2+⌽⍳¯2+≢⍴⍺),(≢⍴⍺)+⍳¯2+≢⍴⍵)⍉(⍺⍉⍨1⌽⌽⍳≢⍴⍺)⍵⍵⍤2⍤2 15⊢⍵⍉⍨(¯2+≢⍴⍵)⌽⍳≢⍴⍵}
out←{(0,⍨(⌽1+⍳¯1+≢⍴⍺),(≢⍴⍺)+⌽⍳¯1+≢⍴⍵)⍉(⍉⍺)⍺⍺⍤1⍤1 15⍉⍵}
pop←{⍺←⊢ ⋄ 1≡⍺1:cry(2↑⍵)mta⍺⍺⍣⍵⍵⊢par⍵ ⋄ cry(2↑⍵)mta⊃⍺⍺⍣⍵⍵/par¨⍺⍵}

spl←{⍉⊂⍤2⊢⍵⍉⍨1⌽⌽⍳⍴⍴⍵}
mix←{⍵⍉⍨1⌽⌽⍳⍴⍴⍵}↑∘⍉

