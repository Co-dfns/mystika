:Namespace SHA

⎕IO ⎕ML ⎕WX←0 1 3

GP←⍳(/⍨)2=0+.=∘.|⍨∘⍳
H0←{↑{⊃⌽{(1|d)((⊃⌽⍵),⌊d←2×⊃⍵)}⍣64-∘⌊⍨⍵⍬*.5⊣⎕FR←1287}¨⍵}
H0∆512←2 4 64⍴H0 GP 20
H0∆384←2 4 64⍴H0 8↓GP 54
H0∆256←2 4 32⍴32↑[2]H0∆512
GH0←{(¯1↓⍴⍵)⍴⍤1 0⊢H0∆256 H0∆384 H0∆512⊃⍨256 384 512⍳⍺}
K0←{{⊃⌽{(1|d)((⊃⌽⍵),⌊d←2×⊃⍵)}⍣64-∘⌊⍨⍵⍬*÷3⊣⎕FR←1287}¨GP ⍵}
K0∆256←⊖↑32↑¨32 K0 313
K0∆512←⊖↑64 K0 410

maj←≠⌿⊢∧1∘⊖            
ch0←(0∘⌷∧1∘⌷)≠2∘⌷>0∘⌷
Sig←{≠⌿⍺⌽⍵⍴⍨3,≢⍵}
sig←{(⍵⊖⍨⊃⍺)≠(⍵⊖⍨1⊃⍺)≠(-≢⍵)↑⍵↓⍨2⊃⍺}

sg0∆256←⍉¯17 ¯19 ¯10 sig⍤1⊢∘.=⍨⍳32
sg1∆256←⍉¯7 ¯18 ¯3 sig⍤1⊢∘.=⍨⍳32
sg0∆512←⍉¯19 ¯61 ¯6 sig⍤1⊢∘.=⍨⍳64                                                                                        
sg1∆512←⍉¯1 ¯8 ¯7 sig⍤1⊢∘.=⍨⍳64
Sg0∆256←⍉¯2 ¯13 ¯22 Sig⍤1⊢∘.=⍨⍳32
Sg1∆256←⍉¯6 ¯11 ¯25 Sig⍤1⊢∘.=⍨⍳32
Sg0∆512←⍉¯28 ¯34 ¯39 Sig⍤1⊢∘.=⍨⍳64
Sg1∆512←⍉¯14 ¯18 ¯41 Sig⍤1⊢∘.=⍨⍳64

c64←{(32↑⍵)((32↓⊢)⍪⍨(32⍴2)⊤2⊥⊣+32↑⊢)(64⍴2)⊤2⊥32↓⍵}
cry←{32=≢⍵:(32⍴2)⊤2⊥⍵ ⋄ c64⍵}

pad←{⍺=256:(⊢(,⍤1)1,(64⍴2)∘⊤∘⊃∘⌽∘⍴↑⍨∘-64+512|∘-65+⊃∘⌽∘⍴)⍵
 (⊢(,⍤1)1,(128⍴2)∘⊤∘⊃∘⌽∘⍴↑⍨∘-128+1024|∘-129+⊃∘⌽∘⍴)⍵}
prs←{⍺=256:⊖(2⌽⍳2+≢⍴⍵)⍉⍵⍴⍨(¯1↓⍴⍵),16 32,⍨512÷⍨⊃⌽⍴⍵
 ⊖(2⌽⍳2+≢⍴⍵)⍉⍵⍴⍨(¯1↓⍴⍵),16 64,⍨1024÷⍨⊃⌽⍴⍵}
w00←{32=1⊃⍴⍵:⍵⍪⍨(32⍴2)⊤2⊥(sg0∆256≠.∧1⌷⍵)+(6⌷⍵)+(sg1∆256≠.∧14⌷⍵)+15⌷⍵
 ⍵⍪⍨c64(sg0∆512≠.∧1⌷⍵)+(6⌷⍵)+(sg1∆512≠.∧14⌷⍵)+15⌷⍵}
wk0←{32=1⊃⍴⍵:(1⌽⍳≢⍴⍵)⍉K0∆256+[0 1]w00⍣48⊢⍵
 (1⌽⍳≢⍴⍵)⍉K0∆512+[0 1]w00⍣64⊢⍵}

t01←{32=1⊃⍴⍵:⍺+(⊢⌿⍵)+(Sg1∆256≠.∧⊣⌿⍵)+ch0 ⍵ ⋄ ⍺+(⊢⌿⍵)+(Sg1∆512≠.∧⊣⌿⍵)+ch0 ⍵}
t02←{32=1⊃⍴⍵:(Sg0∆256≠.∧⊣⌿⍵)+maj 3↑⍵ ⋄ (Sg0∆512≠.∧⊣⌿⍵)+maj 3↑⍵}
f01←{(cry⍤¯1⊢(⍺ t01 ⊢⌿⍵)+⍤15 ¯1⊢(⊢⌿⊣⌿⍵),[¯.5]⍨t02 ⊣⌿⍵)⍪⍤¯1⊢2 3↑⍵}
f02←{cry⍤¯2⊢⍵+⊃f01/(⊂⍤¯1⊢⍺),⊂⍵}
sum←{(¯1⌽⍳≢⍴⍵)⍉⍺↑(⊢⍴⍨⍺,3↓⍴)⊃f02/(⊂⍤¯1⊢⊖wk0 ⍺ prs ⍺ pad ⍵),⊂⍺GH0⍵}

:EndNamespace

:Namespace AES

⎕IO ⎕ML ⎕WX←0 1 3
'cache' 'memo' ⎕CY 'dfns'

ml2←{0⌷⊃(⍺⍺{(⍺⌷⍵)⍺⍺(⍤1)⍵})/(⌽⍺),⊂⍵,[¯0.5]⍨1↑⍨-⍴⍵}
pt2←≠/2|⊢⊖⍨∘-∘⍳∘≢⍨∘.×↑⍨¯1++∘≢⍨∘≢
pr2←{⍺(1↓⊢≠⊢↑⍨∘≢⍨∧∘⊃)⍣(1+(≢⍵)-≢⍺)⊢⍵}
M28←2 0 1⍉1 0 0 0 1 1 0 1 1 pr2⍤1pt2⍤1⍤1 15⍨∘.=⍨⍳8
f28←⊣≠.∧M28≠.∧⊢
inm←⍉(0,⍨7⍴1)f28 ml2⍤1⊢⍉(8⍴2)⊤⍳256
sbf←{(0 1 1 0 0 0 1 1≠[0]⊢≠1∘⊖≠2∘⊖≠3∘⊖≠4∘⊖)inm[;2⊥⍵]}
sbi←{inm[;2⊥(1∘⊖≠3∘⊖≠6∘⊖)0 1 1 0 0 0 1 1≠[0]⍵]}
I00←(8⍴2)⊤⍳256                                                                                                               
sb1←sbf⍤1⊢⍉I00
sb2←sbi⍤1⊢⍉I00
sr1←⍉⍪⍤¯1⊢3 0 1 2⍉M28≠.∧(8⍴2)⊤(⍳16)∘.=,0 ¯1 ¯2 ¯3⊖4 4⍴⍳16
sr2←⍉⍪⍤¯1⊢3 0 1 2⍉M28≠.∧(8⍴2)⊤(⍳16)∘.=,0 1 2 3⊖4 4⍴⍳16
mc0←(-,⍉4×4 4⍴⍳4)⌽16 16⍴4 16↑(-⍳4)⌽4 4∘⍴
mc2←⍉⍪⍤¯1⊢3 0 1 2⍉M28≠.∧(8⍴2)⊤mc0 14 11 13 9
sm1←⍉⍪⍤¯1⊢3 0 1 2⍉M28≠.∧(8⍴2)⊤(mc0 2 3 1 1)+.×(⍳16)∘.=,0 ¯1 ¯2 ¯3⊖4 4⍴⍳16
sm2←⍉⍪⍤¯1⊢3 0 1 2⍉M28≠.∧(8⍴2)⊤(mc0 14 11 13 9)+.×(⍳16)∘.=,0 1 2 3⊖4 4⍴⍳16
rci←10 4 8⍴⍉32↑(8⍴2)⊤1 2 4 8 16 32 64 128 27 54

sbf←sb1≠.∧⍨I00∧.=⍨⊢
sbi←sb2≠.∧⍨I00∧.=⍨⊢
srf←sr1≠.∧⍨⍪
sri←sr2≠.∧⍨⍪
mci←mc2≠.∧⍨⍪
smf←sm1≠.∧⍨⍪
smi←sm2≠.∧⍨⍪ 
rnf←≠⍤2∘smf∘sbf
rni←≠⍤2∘smi∘sbi

kx0←(⌽∘⍳10 8 7⌷⍨128 192 256⍳≢),∘⊂⊢⍴⍨4 8,⍨32÷⍨≢
kx1←{⍺⍺≠¯8:⍵⍪(≠⍀⍺⍺↑⍵)(≠⍤2)(⍺⌷rci)≠sbf 1⊖0⌷¯1↑⍵
 ⍺(⊢⍪(≠⍀4↑¯8↑⊢)≠⍤2∘sbf 0⌷¯1↑⊢)⍺(⊢⍪(≠⍀4↑¯8↑⊢)≠⍤2(rci⌷⍨⊣)≠∘sbf 1⊖0⌷¯1↑⊢)⍵}
kx2←{(16 8,⍨11 13 15⌷⍨128 192 256⍳≢⍵)⍴⊃((¯32÷⍨≢⍵)kx1)/kx0 ⍵}
∆kx ← cache''
kex←kx2 memo ∆kx

kxf←⊖kex
kxi←(¯1∘↑⍪⍨1∘↑⍪∘mci ¯1↓1∘↓)kex
ae0←⊢((16 8,⍨⊢)⍴⊣↑⍨128×⊢)∘⌈128÷⍨×/∘⍴

ecr←{,(kxf ⍺)((0⌷⊣)≠⍤2∘sbf∘srf∘⊃∘(rnf/)((⊂⍤2)1↓¯1↓⊣),∘⊂⊢(≠⍤2)0⌷¯1↑⊣)ae0⍵}
dcr←{,(kxi ⍺)((0⌷⊣)≠⍤2∘sbi∘sri∘⊃∘(rni/)((⊂⍤2)1↓¯1↓⊣),∘⊂⊢(≠⍤2)0⌷¯1↑⊣)ae0⍵}

:EndNamespace
