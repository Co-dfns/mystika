:Namespace SHA

⎕IO ⎕ML←0 1 ⋄ bits←256 384 512

MA←{0⌷[1](((≠/),[0.5](0,⍨1↓∧/))⍣(⊃⍴⍵))⍺,[.5]⍵}
H←{l←⍺⍺{⍵MA ⍤1⊃⍺⍺ Z/(⌽(GK0⍺⍺){⍺⍵}¨↓⍺⍺ W ⍺),⊂⍵}
 ⍺⍺↑,⊃l/(⌽⊂[1 2]⍺⍺P⍵),GH0⍺⍺}
P←{w s m b←(32 512 447 64)(64 1024 895 128)(64 1024 895 128)⊃⍨bits⍳⍺
 (⊢⍴⍨16 w,⍨s÷⍨≢)⍵,1,(0⍴⍨s|m-≢⍵),(b⍴2)⊤≢⍵}
H0←{↑{⊃⌽{(1|d)((⊃⌽⍵),⌊d←2×⊃⍵)}⍣64-∘⌊⍨⍵⍬*.5⊣⎕FR←1287}¨⍵}
GP←{(2=+⌿0=X∘.|X)/X←⍳⍵} ⋄ GH0←{H0∆256 H0∆384 H0∆512⌷⍨bits⍳⍵}
H0∆512←H0 GP 20 ⋄ H0∆384←H0 8↓GP 54 ⋄ H0∆256←32↑[1]H0∆512
S←{⊃≠/⍺⌽¨⊂⍵} ⋄ s←{⊃≠/(⍺⌽¨⊂⍵),⊂(⍺⍺⍴0),(-⍺⍺)↓⍵}
M←{{≠/(-⍳3)(∧/2↑⌽)¨⊂⍵}⍤1⍉3↑⍵} ⋄ C←{((4⌷⍵)∧5⌷⍵)≠(6⌷⍵)∧~4⌷⍵}
W←{x←(¯7 ¯18 3)(¯17 ¯19 10) ⋄ y←(¯1 ¯8 7)(¯19 ¯61 6)
 (a b c)(d e f)←(r←bits⍳⍺⍺)⊃(⊂x),2⍴⊂y ⋄ i←r⊃48 64 64
 {⍵⍪⍉⊃MA/{(d e(f s)14⌷⍵)(9⌷⍵)(a b(c s)1⌷⍵)(0⌷⍵)}¯16↑⍵}⍣i⊢⍵}
K0←{{⊃⌽{(1|d)((⊃⌽⍵),⌊d←2×⊃⍵)}⍣64-∘⌊⍨⍵⍬*÷3⊣⎕FR←1287}¨GP ⍵}
K0∆256←32↑¨32 K0 313 ⋄ K0∆384←K0∆512←64 K0 410
GK0←{K0∆256 K0∆384 K0∆512⊃⍨bits⍳⍵}
Z←{x←(¯2 ¯13 ¯22)(¯6 ¯11 ¯25) ⋄ y←(¯28 ¯34 ¯39)(¯14 ¯18 ¯41)
 a b←(bits⍳⍺⍺)⊃(⊂x),2⍴⊂y
 z←¯1⊖⍵ ⋄ z[4;]←z[4;]MA(t←⊃MA/(b S 4⌷⍵)(C⍵),⍺)MA 0⌷z
 z[0;]←z[0;]MA⊃MA/(a S 0⌷⍵)(M⍵)t ⋄ z}

:EndNamespace

:Namespace AES

⎕IO ⎕ML ⎕WX←0 1 3

ml2←{0⌷⊃(⍺⍺{(⍺⌷⍵)⍺⍺(⍤1)⍵})/(⌽⍺),⊂⍵,[¯0.5]⍨1↑⍨-⍴⍵}
pt2←≠/2|⊢⊖⍨∘-∘⍳∘≢⍨∘.×↑⍨¯1++∘≢⍨∘≢
pr2←{⍺(1↓⊢≠⊢↑⍨∘≢⍨∧∘⊃)⍣(1+(≢⍵)-≢⍺)⊢⍵}
m28←⍉1 0 0 0 1 1 0 1 1 pr2⍤1pt2⍤1⍤1 15⍨∘.=⍨⍳8
f28←{≠/≠/m28∧8 8 8⍴⍺∘.∧⍵}
inm←⍉(0,⍨7⍴1)f28 ml2⍤1⊢⍉(8⍴2)⊤⍳256
sbf←{(0 1 1 0 0 0 1 1≠[0]⊢≠1∘⊖≠2∘⊖≠3∘⊖≠4∘⊖)inm[;2⊥⍵]}
sbi←{inm[;2⊥(1∘⊖≠3∘⊖≠6∘⊖)0 1 1 0 0 0 1 1≠[0]⍵]}
srf←(8 4⍴0 1 2 3)∘⌽
sri←(8 4⍴0 ¯1 ¯2 ¯3)∘⌽
m1f←2 3 1 4 0⍉4 4 4 8 8⍴(∘.=⍨⍳8)f28⍤1⍤15 1⊢⍉(8⍴2)⊤(-⍳4)⌽4 4⍴2 3 1 1
m1i←2 3 1 4 0⍉4 4 4 8 8⍴(∘.=⍨⍳8)f28⍤1⍤15 1⊢⍉(8⍴2)⊤(-⍳4)⌽4 4⍴14 11 13 9
mcf←{≠/≠/m1f∧8 4 4 4 8⍴⍉⍵}
mci←{≠/≠/m1i∧8 4 4 4 8⍴⍉⍵}
rci←(⍉⍤2)10 4 8⍴⍉32↑(8⍴2)⊤1 2 4 8 16 32 64 128 27 54
rnf←≠∘mcf∘srf∘sbf
rni←mci≠∘sbi∘sri
kx0←(⌽∘⍳10 8 7⌷⍨128 192 256⍳≢),∘⊂0 2 1⍉⊢⍴⍨4 8,⍨32÷⍨≢
kx1←{⍺⍺≠¯8:⍵⍪(≠⍀⍺⍺↑⍵)(≠⍤2)(⍺⌷rci)≠sbf 1⌽0⌷¯1↑⍵
 ⍺(⊢⍪(≠⍀4↑¯8↑⊢)≠⍤2∘sbf 0⌷¯1↑⊢)⍺(⊢⍪(≠⍀4↑¯8↑⊢)≠⍤2(rci⌷⍨⊣)≠∘sbf 1⌽0⌷¯1↑⊢)⍵}
kex←{0 3 1 2⍉(4 8 4,⍨11 13 15⌷⍨128 192 256⍳≢⍵)⍴⊃((¯32÷⍨≢⍵)kx1)/kx0 ⍵}   
ecr←{,⍉(⊖kex ⍺)((0⌷⊣)≠∘sbf∘srf∘⊃∘(rnf/⊂⍤3)1↓(¯1↓⊣)⍪⊢≠0⌷¯1↑⊣)⍉4 4 8⍴⍵}
dcr←{,⍉(kex ⍺)((0⌷⊣)≠∘sbi∘sri∘⊃∘(rni/⊂⍤3)1↓(¯1↓⊣)⍪⊢≠0⌷¯1↑⊣)⍉4 4 8⍴⍵}

:EndNamespace